dnl Process this file with autoconf to produce a configure script.

dnl Initialise.   Look for sccsfile.h to double-check that we are looking
dnl at the correct directory.
AC_INIT(sccsfile.h)


AM_INIT_AUTOMAKE(cssc,0.03-pl0)

dnl AC_CONFIG_HEADER(config.h.)
AM_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_SUBST(GXX)dnl
AC_PROG_CPP
AC_PROG_CXXCPP
AM_PROG_INSTALL
AC_PROG_RANLIB

AC_PATH_PROG(PR,pr,/usr/bin/pr)
dnl It would be a good idea to default the path for diff
dnl to install_dir/diff-program so the installer can 
dnl add a symlink later.   TODO.
AC_PATH_PROG(CONFIG_DIFF_COMMAND,diff,/usr/bin/diff)
AC_DEFINE_UNQUOTED(CONFIG_DIFF_COMMAND,"$ac_cv_path_CONFIG_DIFF_COMMAND")

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/stat.h sys/param.h sys/types.h sys/file.h)
AC_CHECK_HEADERS(fcntl.h prototypes.h io.h process.h pwd.h unistd.h)
AC_CHECK_HEADERS(stdarg.h stdlib.h string.h stdio.h errno.h)
AC_HEADER_DIRENT
AC_HEADER_SYS_WAIT

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_TYPE_PID_T
AC_TYPE_UID_T


dnl AC_CHECK_DECL_IN_HEADER(IDENTIFIER, HEADERS)
AC_DEFUN(AC_CHECK_DECL_IN_HEADER,
AC_MSG_CHECKING(for $1 in $2)
ac_tr_ident=`echo $1 | tr '.abcdefghijklmnopqrstuvwxyz' '_ABCDEFGHIJKLMNOPQRSTUVWXYZ'`
ac_tr_header=`echo $2 | tr '.abcdefghijklmnopqrstuvwxyz' '_ABCDEFGHIJKLMNOPQRSTUVWXYZ'`
ac_tr_define=${ac_tr_header}_DECLARES_${ac_tr_ident}
AC_CACHE_VAL(ac_cv_decl_$ac_tr_define,
[AC_EGREP_HEADER([$1], [$2], 
eval "ac_cv_decl_$ac_tr_define=yes", 
eval "ac_cv_decl_$ac_tr_define=no")])dnl
if eval "test \"`echo '$ac_cv_decl_'$ac_tr_define`\" = yes"; then
	 AC_MSG_RESULT(yes)
	 echo 'defining' $ac_tr_define
	 AC_DEFINE_UNQUOTED($ac_tr_define)
else
	 echo 'NOT defining' $ac_tr_define 
	 AC_MSG_RESULT(no)
fi
)



dnl Look for declarations in headers.


dnl Checks for library functions.
dnl AC_FUNC_MEMCMP
dnl The "select" that autoscan found was a method. AC_CHECK_FUNCS(select)

AM_FUNC_MKTIME
dnl AC_CHECK_FUNCS(mktime abort wait)

dnl Check for symlink() and readlink(), which lndir uses.
AC_CHECK_FUNCS(symlink readlink) 

AC_CHECK_FUNCS(abort wait)
AC_CHECK_FUNCS(stat getpwuid getlogin setreuid pipe spawn geteuid getegid)
AC_CHECK_FUNCS(fsetpos)
AC_REPLACE_FUNCS(strerror remove rename strstr)



dnl
dnl On AmigsOS, fork() is a stub (in ixemul.library).  This means that
dnl AC_CHECK_FUNC will find it and so unless we handle it specially, 
dnl configure will set up for using fork().  For AmigaOS, we will probably
dnl use system().  On AmigaOS system() is not going to be a security hole,
dnl since there is no concept of separate users and so no setuid execution,
dnl which is the potential problem with system, and the reason we don't
dnl use it by default.
dnl
AC_MSG_CHECKING([for AmigaOS target])
AC_EGREP_CPP(yes,
[#ifdef __amigaos__
       yes
#endif
], AC_MSG_RESULT([avoiding fork() on AmigaOS]),
AC_MSG_RESULT(no)
AC_CHECK_FUNC(fork,AC_DEFINE_UNQUOTED(HAVE_FORK)))



AC_SUBST(LIBOBJS)dnl



AC_DEFUN(AC_CHECK_GLOBAL,
[for ac_global in $1
do
   ac_tr_global=HAVE_`echo $ac_global | tr 'abcdefghijklmnopqrstuvwxyz' 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'`
   AC_MSG_CHECKING([for global variable ${ac_global}])
   AC_CACHE_VAL(ac_cv_global_$ac_global,
   [
    AC_TRY_LINK(dnl
    [/* no includes */],
    [ extern long int $ac_global;  exit((int)$ac_global)],
    eval "ac_cv_global_${ac_global}=yes",
    eval "ac_cv_global_${ac_global}=no"
    )
dnl   ]
   )
  if eval "test \"`echo '$ac_cv_global_'$ac_global`\" = yes"; then
    AC_MSG_RESULT(yes)
    AC_DEFINE_UNQUOTED($ac_tr_global)
  else
    AC_MSG_RESULT(no)
  fi
done
])


dnl Search for the global variables timezone, sys_nerr, sys_errlist.

dnl Cope with not having a mktime() function
if test "$ac_cv_func_mktime" != yes; then
AC_CHECK_GLOBAL(timezone)
fi

dnl Cope with not having a strerror() function
if test "$ac_cv_func_strerror" != yes; then
AC_CHECK_GLOBAL(sys_nerr)
AC_CHECK_GLOBAL(sys_errlist)
AC_CHECK_DECL_IN_HEADER(sys_errlist,errno.h)
AC_CHECK_DECL_IN_HEADER(sys_errlist,stdlib.h)
AC_CHECK_DECL_IN_HEADER(sys_errlist,stdio.h)

AC_CHECK_DECL_IN_HEADER(sys_nerr,errno.h)
AC_CHECK_DECL_IN_HEADER(sys_nerr,stdlib.h)
AC_CHECK_DECL_IN_HEADER(sys_nerr,stdio.h)
fi

dnl What about saved setuid?
dnl AC_CONFIG_SUBDIRS(testutils)
dnl AC_OUTPUT(shorter/Makefile)

dnl *** NOTE:
dnl There can be only ONE call to AC_OUTPUT in each configure.in,
dnl otherwise you get an error when you run configure.  AC_OUTPUT
dnl deletes confdefs.h and to the second invocation couldn't find it
dnl and so things go wrong.

AC_OUTPUT(Makefile sccsdiff.sh tests/Makefile docs/Makefile testutils/Makefile version.cc)
