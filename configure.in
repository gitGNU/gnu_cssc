dnl Process this file with autoconf to produce a configure script.

dnl Initialise.   Look for sccsfile.h to double-check that we are looking
dnl at the correct directory.
AC_INIT(sccsfile.h)

dnl Checks for programs.
dnl AC_PROG_CC
AC_PROG_CXX
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING(whether g++ is being used)
AC_SUBST(GXX)dnl
AC_MSG_RESULT($GXX)
AC_PROG_INSTALL
dnl It would be a good idea to defailt the path for diff
dnl to install_dir/diff-program so the installer can 
dnl add a symlink later.   TODO.
AC_PATH_PROG(CONFIG_DIFF_COMMAND,diff,/usr/bin/diff)
AC_DEFINE_UNQUOTED(CONFIG_DIFF_COMMAND,"$ac_cv_path_CONFIG_DIFF_COMMAND")

dnl Checks for libraries.

dnl Checks for header files.
AC_CONFIG_HEADER(aconf.h)
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/types.h sys/file.h prototypes.h io.h process.h pwd.h unistd.h)
AC_HEADER_DIRENT
AC_HEADER_SYS_WAIT

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.
dnl AC_FUNC_MEMCMP
dnl The "select" that autoscan found was a method. AC_CHECK_FUNCS(select)
AC_CHECK_FUNCS(mktime remove rename strerror stat getpwuid getlogin setreuid abort)
AC_CHECK_FUNCS(fork pipe spawn strstr)
AC_CHECK_FUNCS(wait)

AC_DEFUN(AC_CHECK_GLOBAL,
[for ac_global in $1
do
   ac_tr_global=HAVE_`echo $ac_global | tr 'abcdefghijklmnopqrstuvwxyz' 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'`
   AC_MSG_CHECKING([for global variable ${ac_global}])
   AC_CACHE_VAL(ac_cv_global_$ac_global,
   [
    AC_TRY_LINK(dnl
    [/* no includes */],
    [ extern long int $ac_global;  exit((int)$ac_global)],
    eval "ac_cv_global_${ac_global}=yes",
    eval "ac_cv_global_${ac_global}=no"
    )
dnl   ]
   )
  if eval "test \"`echo '$ac_cv_global_'$ac_global`\" = yes"; then
    AC_MSG_RESULT(yes)
    AC_DEFINE_UNQUOTED($ac_tr_global)
  else
    AC_MSG_RESULT(no)
  fi
done
])

dnl Search for the global variable timezone.
AC_CHECK_GLOBAL(timezone)

dnl What about saved setuid?
AC_OUTPUT(Makefile)
